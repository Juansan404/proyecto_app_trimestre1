plugins {
  id 'application'
  id 'org.openjfx.javafxplugin' version '0.1.0' //Plugin que ayuda a construir proyectos JAVAFX
}

repositories {
	mavenCentral()
}

javafx {
    version = "25"
    modules = [ 'javafx.base', 'javafx.swing', 'javafx.graphics', 'javafx.controls', 'javafx.fxml', 'javafx.media', 'javafx.web']
}

dependencies {
    // MySQL JDBC Driver para conectar con la base de datos
    implementation 'com.mysql:mysql-connector-j:8.2.0'

    // ControlsFX para validaciones
    implementation 'org.controlsfx:controlsfx:11.2.2'

    // PDFfx para visualizar PDFs
    implementation 'com.dlsc.pdfviewfx:pdfviewfx:3.4.1'

    // JasperReports para generación de informes
    implementation 'net.sf.jasperreports:jasperreports:7.0.1'
    implementation 'net.sf.jasperreports:jasperreports-pdf:7.0.1'
    implementation 'net.sf.jasperreports:jasperreports-charts:7.0.1'
}

java { //Se indica la versión de JDK que ha de utilizar
    toolchain {
        languageVersion = JavaLanguageVersion.of(24)
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application { //Definimos clase principal del proyecto
    mainClass = 'com.javafx.Main'
}

run {
    jvmArgs = [
        '--add-exports', 'javafx.graphics/com.sun.javafx.sg.prism=ALL-UNNAMED',
        '--add-exports', 'javafx.graphics/com.sun.javafx.sg.prism.web=ALL-UNNAMED',
        '--add-exports', 'javafx.web/com.sun.javafx.webkit=ALL-UNNAMED',
        '--add-exports', 'javafx.web/com.sun.webkit=ALL-UNNAMED',
        '--add-exports', 'javafx.web/com.sun.javafx.webkit.prism=ALL-UNNAMED',
        '--add-opens', 'javafx.graphics/com.sun.javafx.sg.prism=ALL-UNNAMED',
        '--add-opens', 'javafx.graphics/javafx.scene=ALL-UNNAMED',
        '--add-opens', 'javafx.web/javafx.scene.web=ALL-UNNAMED'
    ]
}

sourceSets {
  main {
    resources {
        srcDirs = ["src/main/resources"]
        includes = ["**/*.fxml","**/*.png","**/*.properties","**/*.css","**/*.pdf","**/*.jrxml","**/*.jasper","**/*.gif","**/*.jpg","**/*.ico"]  //Importante para poder abrir una extensión no reconocida
    }
  }
}

// Tarea para copiar todas las dependencias a la carpeta build/libs/lib
tasks.register('copyDependencies', Copy) {
    from configurations.runtimeClasspath
    into "$buildDir/libs/lib"
}

// Configuración del JAR normal
tasks.jar {
    dependsOn copyDependencies // Hace que siempre se copien las librerías al hacer el jar

    manifest {
        attributes(
            'Main-Class': application.mainClass.get(),
            //Esto crea el "Class-Path" dentro del manifiesto del JAR
            'Class-Path': configurations.runtimeClasspath.files.collect { "lib/" + it.name }.join(' ')
        )
    }
}





